# -*- mode: shell-script; sh-shell: zsh; fill-column: 75; comment-column: 50; coding: utf-8 -*-

emulate -L zsh

local expr head
local PROMPT4 PS4="; "

if [[ "$*" =~ "^(-[a-zA-Z] |=[0-9]+ )?[oO]?[t0-9a-fA-Fx.]+[oO]?$" ]]; then
    { setopt xtrace; rax2 "$@" }
else
    expr=''

    while (( $# )); do
        head="$1"
        shift

        # Convert `100_000_000` to `100000000`
        if [[ "$head" =~ '^[0-9]+(_[0-9]{1,3})*_[0-9]+$' ]]; then
            head="${head//_/}"
        fi

        print -v expr -f "%s %s" -- "$expr" "$head"

        if [[ "$head" =~ "^[a-zA-Z]+$" && "$1" =~ "^[^\(]" ]]; then
            print -v expr -f "%s(%s)" -- "$expr" "$1"
            shift
        fi
    done
    expr="${expr## }"

    { setopt xtrace; emacsclient -e "(calc-eval \"$expr\")" } | perl -wple 's/^"|"$//g'
fi
